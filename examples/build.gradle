group "pl.mateuszchudyk.jmath"

dependencies {
    compile project(":jmath")
}

def getPackageDir() {
    return project.group.replace(".", "/") + "/" + project.name
}

def getExamplesList() {
    def examples = []
    fileTree(dir: "src/main/java/" + getPackageDir(), include: "**/*.java").each { f->
        examples << f.name.take(f.name.lastIndexOf("."))
    }
    return examples
}

getExamplesList().each { name ->
    tasks.create(name: "create${name}", type: Jar) {
        manifest {
            attributes "Main-Class": "${project.group}.examples.${name}"
            attributes "Class-Path": "jmath-1.0.0.jar"
        }
        from("${project.buildDir}/classes/java/main/${getPackageDir()}/${name}.class") {
            into("${getPackageDir()}")
        }
        baseName name
    }
}

build {
    getExamplesList().each { name ->
        dependsOn "create${name}"
    }
}

task copyExamplesToOutputDir(type: Copy) {
    dependsOn(":jmath:copyLibraryToOutputDir")

    from("${project.buildDir}/libs/") {
        getExamplesList().each { name ->
            include "${name}.jar"
        }
    }
    into "${project.rootProject.projectDir}/output/"
}

build.finalizedBy(copyExamplesToOutputDir)
